#Locations depicted in Welsh Landscape Collection, on map
#defaultView:Map{"hide":["?geo", "?rgb", "?layer"],"markercluster": true}
SELECT DISTINCT ?geo ?subj ?depicts 
(SAMPLE(?sLabel) AS ?subjLabel) 
(SAMPLE(?dLabel) AS ?depictsLabel)
(SAMPLE(?pic) AS ?pic) 
(SAMPLE(?layer) AS ?layer) 
(SAMPLE(?rgb) AS ?rgb)  
WHERE {  
  ?subj wdt:P361 wd:Q21542493 ;
        wdt:P180 ?depicts .
        OPTIONAL{ ?subj wdt:P18 ?pic . } 
  ?depicts wdt:P3896 ?geo ;
           wdt:P31 ?is .  
           VALUES ?is { wd:Q23413 wd:Q160742 wd:Q2977 wd:Q39715 wd:Q16970 wd:Q19844914 wd:Q1802963 wd:Q16884952 }
  BIND(
    IF(?is = wd:Q23413, "castle",
    IF(?is = wd:Q160742, "abbey",
    IF(?is = wd:Q2977, "cathedral",
    IF(?is = wd:Q39715, "lighthouse",
    IF(?is = wd:Q16970, "church",  
    IF(?is = wd:Q19844914, "college",
    IF(?is = wd:Q1802963, "country house",
    IF(?is = wd:Q16884952, "country house",
           "")))))))) AS ?layer).  
  BIND(
    IF(?is = wd:Q23413, "FF0000",
    IF(?is = wd:Q160742, "0080FF",
    IF(?is = wd:Q2977, "00FF00",
    IF(?is = wd:Q39715, "FF00FF",
    IF(?is = wd:Q16970, "FFFF00",  
    IF(?is = wd:Q19844914, "808080",
    IF(?is = wd:Q1802963, "80ffff",
    IF(?is = wd:Q16884952, "80ffff",
           "")))))))) AS ?rgb). 
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". 
         ?subj rdfs:label ?sLabel .                
         ?depicts rdfs:label ?dLabel .   }
}
GROUP BY ?geo ?subj ?depicts
